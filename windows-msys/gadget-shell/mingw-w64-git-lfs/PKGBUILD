# Maintainer: Brendan Forster <brendan@github.com>

_realname="git-lfs"
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.0.1
pkgrel=1
pkgdesc="An open source Git extension for versioning large files"
install=git-lfs.install
arch=('any')
url="https://github.com/git-lfs/git-lfs"
src_zip_url="${url}/archive/v${pkgver}.zip"
license=('MIT')
groups=('VCS')

case "$CARCH" in
i686)
  folder="git-lfs-windows-386-$pkgver"
  sha256sum=c5094dadfbc412c9ef325e3bfc93940768e8031653b30e3aa7ed3a13040cfdec
  ;;
x86_64)
  folder="git-lfs-windows-amd64-$pkgver"
  sha256sum=83d9283250b96034628e380271f0e6360aa6cabdb637acb145fb4c360aafe53a
  ;;
esac

source=("https://github.com/github/git-lfs/releases/download/v$pkgver/$folder.zip"
	"$src_zip_url")

# Git LFS' source .zip now contains symbolic links that bsdtar does not like...
noextract=("${src_zip_url##*/}")

sha256sums=("$sha256sum" SKIP)
options=('!strip')

package() {
  install -d -m755 $pkgdir/$MINGW_PREFIX/bin
  install -m755 $srcdir/$folder/git-lfs.exe $pkgdir/$MINGW_PREFIX/bin/git-lfs.exe
}
